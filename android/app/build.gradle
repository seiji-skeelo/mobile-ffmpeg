apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 29
    ndkVersion "21.3.6528147"

    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 29
        versionCode 240442
        versionName "4.4.2"
        project.archivesBaseName = "mobile-ffmpeg"
        consumerProguardFiles 'proguard-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ["../libs"]
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

task javadoc(type: Javadoc) {
    title = 'MobileFFmpeg'
    destinationDir = file("${projectDir}/../../docs/android/javadoc")
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    source = android.sourceSets.main.java.srcDirs
}

dependencies {
    testImplementation "androidx.test.ext:junit:1.1.1"
    testImplementation "org.json:json:20190722"
}

// Configuração para publicação no JitPack
afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                
                groupId = 'com.arthenica'
                artifactId = 'mobile-ffmpeg-full'
                version = '4.4.2'
                
                // Adiciona os arquivos nativos (.so) na publicação
                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')
                    
                    configurations.implementation.allDependencies.each {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}
