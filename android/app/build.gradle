apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 29
    ndkVersion "21.3.6528147"
    namespace "com.arthenica.mobileffmpeg"
    
    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 29
        versionCode 240442
        versionName "4.4.2"
        project.archivesBaseName = "mobile-ffmpeg"
        consumerProguardFiles 'proguard-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    
    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ["../libs"]
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

task javadoc(type: Javadoc) {
    title = 'MobileFFmpeg'
    destinationDir = file("${projectDir}/../../docs/android/javadoc")
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    source = android.sourceSets.main.java.srcDirs
}

dependencies {
    testImplementation "androidx.test.ext:junit:1.1.1"
    testImplementation "org.json:json:20190722"
}

// Configuração para publicação no JitPack
publishing {
    publications {
        release(MavenPublication) {
            groupId = 'com.arthenica'
            artifactId = 'mobile-ffmpeg-full'
            version = '4.4.2'
            
            afterEvaluate {
                from components.release
            }
            
            // Adiciona os arquivos nativos (.so) na publicação
            pom {
                name = 'mobile-ffmpeg'
                description = 'FFmpeg for Android'
                url = 'https://github.com/seiji-skeelo/mobile-ffmpeg'
                licenses {
                    license {
                        name = 'GNU Lesser General Public License, Version 3.0'
                        url = 'https://www.gnu.org/licenses/lgpl-3.0.en.html'
                    }
                }
            }
        }
    }
}
